! Split comm world into n disjoint comms
! The processes are divided evenly across the n comms
! gcomm_idx is a global index (among the comms) of the new_comm
! Note: The function assumes that there are atleast ncomms
! procs
SUBROUTINE split_world_ncomms(ncomms, new_comm, new_rank, new_size, gcomm_idx)
  use mpi
  use pio_tutil
  implicit none
  integer, intent(inout) :: ncomms
  integer, intent(inout) :: new_comm
  integer, intent(inout) :: new_rank
  integer, intent(inout) :: new_size
  integer, intent(inout) :: gcomm_idx

  integer :: nprocs_per_comm
  logical :: is_last_comm
  integer :: ierr
  integer :: color

  new_comm = MPI_COMM_NULL
  new_rank = 0
  new_size = 0
  gcomm_idx = 0

  ! We need at least one proc in each disjoint comm
  if(pio_tf_world_sz_ < ncomms) then
    return
  end if

  nprocs_per_comm = pio_tf_world_sz_/ncomms
  is_last_comm = .false.
  if(pio_tf_world_rank_ >= nprocs_per_comm * (ncomms - 1)) then
    is_last_comm = .true.
  end if

  color = pio_tf_world_rank_/nprocs_per_comm
  ! Make sure that every proc in the last set (all remaining procs
  ! are included in the last set/comm) gets the same color
  if(is_last_comm) then
    color = ncomms - 1
  end if
  gcomm_idx = color + 1

  call MPI_Comm_split(pio_tf_comm_, color, 0, new_comm, ierr)

  call MPI_Comm_size(new_comm, new_size, ierr)
  call MPI_Comm_rank(new_comm, new_rank, ierr)
END SUBROUTINE split_world_ncomms

! Async I/O as service test with 2 compute comms and 1 io comm
! The test writes data into two files, All info/data related
! to the first file is provided by the 1st compute comm and
! all info/data related to the second file is provided by the
! 2nd compute comm. Both compute comms share a single io comm
PIO_TF_TEMPLATE<PIO_TF_PREDEF_TYPENAME PIO_TF_DATA_TYPE, PIO_TF_PREDEF_TYPENAME PIO_TF_FC_DATA_TYPE>
PIO_TF_AUTO_TEST_SUB_BEGIN async_2comp_wr_1d
  use mpi
  implicit none
  ! Number of compute components
  integer, parameter :: NUM_COMPONENTS = 2
  ! Total number of comms = comms for each of NUM_COMPONENTS + io comm
  integer :: ncomms = NUM_COMPONENTS + 1
  ! We need to have at least one proc in each of comm
  integer, parameter :: MIN_NUM_PROCS = NUM_COMPONENTS + 1
  integer :: comp_comms(NUM_COMPONENTS)
  type(iosystem_desc_t) :: iosys(NUM_COMPONENTS)
  integer :: new_comm, io_comm
  integer :: new_rank, new_size, i, j
  ! gcomm_idx => Global index for the MPI comms
  ! gcomp_idx => Global index for the compute components
  integer :: gcomm_idx, gcomp_idx
  ! is_io == .true. if proc is part of the io comp
  logical :: is_io = .false.
  integer, parameter :: NUM_REARRANGERS = 2
  integer :: rearrs(NUM_REARRANGERS) = (/pio_rearr_subset,pio_rearr_box/)
  character(len=PIO_TF_MAX_STR_LEN) :: rearrs_info(NUM_REARRANGERS) = (/"PIO_REARR_SUBSET","PIO_REARR_BOX   "/)

  ! file1 is written out by comp1 and file2 is written out by comp2
  character(len=PIO_TF_MAX_STR_LEN) :: fnames(NUM_COMPONENTS) = (/"pio_async_decomp_tests_file_comp1.nc","pio_async_decomp_tests_file_comp2.nc"/)
  character(len=PIO_TF_MAX_STR_LEN) :: dimname = "PIO_TF_test_dim"
  character(len=PIO_TF_MAX_STR_LEN) :: vname = "PIO_TF_test_var"

  ! The size of vec/data written out from each compute proc
  integer, parameter :: VEC_LOCAL_SZ = 7
  integer, parameter :: NUM_DIMS = 1
  type(var_desc_t) :: pio_var
  type(file_desc_t) :: pio_file
  type(io_desc_t) :: iodesc
  integer :: dims(NUM_DIMS), pio_dim
  integer, dimension(VEC_LOCAL_SZ) :: compdof, compdof_rel_disps
  PIO_TF_FC_DATA_TYPE, dimension(VEC_LOCAL_SZ) :: wbuf, rbuf

  integer, dimension(:), allocatable :: iotypes
  character(len=PIO_TF_MAX_STR_LEN), dimension(:), allocatable :: iotype_descs
  integer :: num_iotypes = 0

  integer :: ret = PIO_NOERR

  num_iotypes = 0
  call PIO_TF_Get_nc_iotypes(iotypes, iotype_descs, num_iotypes)

  do i=1,VEC_LOCAL_SZ
    compdof_rel_disps(i) = i
  end do

  do i=1,NUM_REARRANGERS
    PIO_TF_LOG(0,*) "Testing rearr : ", trim(rearrs_info(i))
    if(pio_tf_world_sz_ >= MIN_NUM_PROCS) then
      ! Create three disjoint sets of IO procs and compute procs
      call split_world_ncomms(ncomms, new_comm, new_rank, new_size, gcomm_idx)
      !print *, "Split new comm, size = ", new_size, ", rank = ", new_rank, ", gcomm_idx = ", gcomm_idx 

      do j=1,NUM_COMPONENTS
        comp_comms(j) = MPI_COMM_NULL
      end do

      ! Use first comm as the IO proc
      is_io = .false.
      if(gcomm_idx == 1) then
        is_io = .true.
      end if
      gcomp_idx = gcomm_idx - 1

      if(is_io) then
        ! I/O proc
        io_comm = new_comm
        call PIO_init(NUM_COMPONENTS, pio_tf_comm_, comp_comms, io_comm,&
              iosys, rearrs(i))
      else
        ! Compute proc
        comp_comms(gcomp_idx) = new_comm
        io_comm = MPI_COMM_NULL
        call PIO_init(NUM_COMPONENTS, pio_tf_comm_, comp_comms, io_comm,&
              iosys, rearrs(i))

        ! Only compute procs send data 
        dims(1) = VEC_LOCAL_SZ * new_size
        compdof = VEC_LOCAL_SZ * new_rank + compdof_rel_disps
        wbuf = pio_tf_world_rank_

        ! Only compute procs call PIO function calls, I/O proc
        ! waits inside PIO_init() waiting for commands from the
        ! compute processes
        call PIO_initdecomp(iosys(gcomp_idx), PIO_TF_DATA_TYPE, dims, compdof, iodesc)
        !print *, "iosys idx = ", gcomp_idx, " = ", iosys(gcomp_idx)
        do j=1,num_iotypes
          PIO_TF_LOG(0,*) "Testing : PIO_TF_DATA_TYPE : ", iotype_descs(i)

          ret = PIO_createfile(iosys(gcomp_idx), pio_file, iotypes(j), trim(fnames(gcomp_idx)), PIO_CLOBBER)
          PIO_TF_CHECK_ERR(ret, new_comm, "Could not create file :" // trim(fnames(gcomp_idx)))

          ret = PIO_def_dim(pio_file, trim(dimname), dims(1), pio_dim)
          PIO_TF_CHECK_ERR(ret, new_comm, "Could not define dim :" // trim(fnames(gcomp_idx)))

          ret = PIO_def_var(pio_file, trim(vname), PIO_TF_DATA_TYPE, (/pio_dim/), pio_var) 
          PIO_TF_CHECK_ERR(ret, new_comm, "Could not define var :" // trim(fnames(gcomp_idx)))

          ret = PIO_enddef(pio_file)
          PIO_TF_CHECK_ERR(ret, new_comm, "Could not enddef in file :" // trim(fnames(gcomp_idx)))

          call PIO_write_darray(pio_file, pio_var, iodesc, wbuf, ret)
          PIO_TF_CHECK_ERR(ret, new_comm, "Write darray failed :" // trim(fnames(gcomp_idx)))

#ifdef PIO_TEST_CLOSE_OPEN_FOR_SYNC
          call PIO_closefile(pio_file)

          ret = PIO_openfile(iosys(gcomp_idx), pio_file, iotypes(j), trim(fnames(gcomp_idx)), PIO_nowrite)
          PIO_TF_CHECK_ERR(ret, new_comm, "Could not reopen file :" // trim(fnames(gcomp_idx)))

          ret = PIO_inq_varid(pio_file, trim(vname), pio_var)
          PIO_TF_CHECK_ERR(ret, new_comm, "Could not inq var:" // trim(fnames(gcomp_idx)))
#else
          call PIO_syncfile(pio_file)
#endif

          rbuf = -1
          call PIO_read_darray(pio_file, pio_var, iodesc, rbuf, ret)
          PIO_TF_CHECK_ERR(ret, new_comm, "Could read var :" // trim(fnames(gcomp_idx)))

          ! FIXME: We don't have a PIO_TF_CHECK_VAL for a comm
          !PIO_TF_CHECK_VAL((rbuf, wbuf), "Got wrong val")
          PIO_TF_ASSERT(ALL(rbuf == wbuf), "Got wrong val")

          call PIO_closefile(pio_file)
          call PIO_deletefile(iosys(gcomp_idx), fnames(gcomp_idx))
        end do
        call PIO_freedecomp(iosys(gcomp_idx), iodesc)

        do j=1,NUM_COMPONENTS
          call PIO_finalize(iosys(j), ret)
        end do
      end if
      call MPI_Comm_free(new_comm, ret)
      PIO_TF_CHECK_ERR(ret, "PIO_init()/Finalize() failed")
    end if
  end do
PIO_TF_AUTO_TEST_SUB_END async_2comp_wr_1d

